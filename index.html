<!DOCTYPE html>
<html lang="ja">
<head>
  <meta charset="UTF-8">
  <title>v1.18_install_v2.1.bat</title>
</head>
<body>
<pre>
@echo off
rem Naruhiko Ueda 2023-06-11 v3.0

rem 管理者実行
openfiles > nul 2>&1
if not %ERRORLEVEL% == 0 (
  powershell start-process \"%~f0\" -Verb runas
  goto exit
)

chcp 65001 > nul

echo マインクラフトを終了...
msiexec.exe /x {8B25A747-79B3-4D9B-B969-DA4DF0627110} AI_UNINSTALLER_CTP=1 /passive

echo v1.18をダウンロード...
set "url1=https://d1pjd0t9x8mnxp.cloudfront.net/MinecraftEducation_x86_1.18.45.0.msi"
set "url2=https://d1pjd0t9x8mnxp.cloudfront.net/MinecraftEducation_x86_1.18.45.01.cab"
set "url3=https://d1pjd0t9x8mnxp.cloudfront.net/vc_redist.x86.exe"
if not exist "%TEMP%\v1.18_install" (mkdir "%TEMP%\v1.18_install")
curl -k -L -o "%TEMP%\v1.18_install\MinecraftEducation_x86_1.18.45.0.msi" "%url1%"
curl -k -L -o "%TEMP%\v1.18_install\MinecraftEducation_x86_1.18.45.01.cab" "%url2%"
curl -k -L -o "%TEMP%\v1.18_install\vc_redist.x86.exe" "%url3%"
echo v1.18ダウンロード完了

echo マインクラフトを終了...
taskkill /F /T /IM Minecraft.Windows.exe
echo v1.18インストール...
cd "%TEMP%\v1.18_install"
start "" "vc_redist.x86.exe" /install /passive
msiexec /i MinecraftEducation_x86_1.18.45.0.msi /passive

echo アップデートスケジューラを削除...
schtasks /delete /tn "Minecraft Education Automatic Updater" /f
schtasks /delete /tn "Minecraft Education Weekly Updater" /f

echo アップデートプログラムをリネーム...
cd "C:\Program Files (x86)\Microsoft Studios\Minecraft Education Edition"
if exist "MinecraftEducationUpdater.exe.org" del "MinecraftEducationUpdater.exe.org"
ren "MinecraftEducationUpdater.exe" "MinecraftEducationUpdater.exe.org"

set "directory=%USERPROFILE%\AppData\Roaming\Minecraft Education Edition\games\com.mojang\minecraftWorlds"

echo マインクラフトを終了...
taskkill /F /T /IM Minecraft.Windows.exe

for /d %%i in ("%directory%\*") do (
  setlocal enabledelayedexpansion
  set "subdir=%%~nxi"
  if exist "%%i\levelname.txt" (
    type "%%i\levelname.txt"
    echo.
    echo !subdir!
    echo ワールド処理中...
    echo.
    call :world_convert "!directory!\!subdir!"
  )
  endlocal
)
echo 処理が完了しました！
echo Minecraft Educationを起動します...
start "" "C:\Program Files (x86)\Microsoft Studios\Minecraft Education Edition\Minecraft.Windows.exe"
del %0
exit

:world_convert
@echo off
setlocal

set "FILENAME=%~1\level.dat"

REM バックアップ作成
copy "%FILENAME%" "%FILENAME%.org" > NUL

REM 16進数に変換
REM certutil -f -encodehex "%FILENAME%" "%FILENAME%.hex"
powershell -Command "$bytes = [System.IO.File]::ReadAllBytes('"%FILENAME%"');$hexContent = [System.BitConverter]::ToString($bytes);$hexContent | Out-File -Encoding ASCII -NoNewline '"%FILENAME%.hex"'"

REM level.dat.hex 書き換え
powershell -Command "$content = Get-Content -Encoding ASCII '"%FILENAME%.hex"' -Raw; $content = $content.Remove(0, 11); $content = $content.Insert(0, '09-00-00-00'); [System.IO.File]::WriteAllText('"%FILENAME%.hex"', $content)"
REM InventoryVersion
powershell -Command "$content = Get-Content -Encoding ASCII '"%FILENAME%.hex"' -Raw; $index = $content.IndexOf('49-6E-76-65-6E-74-6F-72-79-56-65-72-73-69-6F-6E-'); $content = $content.Remove($index+48, 35);$content = $content.Insert($index+48, '07-00-31-2E-31-38-2E-34-35-01-0C-00'); [System.IO.File]::WriteAllText('"%FILENAME%.hex"', $content)"
REM StorageVersion
powershell -Command "$content = Get-Content -Encoding ASCII '"%FILENAME%.hex"' -Raw; $index = $content.IndexOf('53-74-6F-72-61-67-65-56-65-72-73-69-6F-6E-'); $content = $content.Remove($index+42, 11);$content = $content.Insert($index+42, '09-00-00-00'); [System.IO.File]::WriteAllText('"%FILENAME%.hex"', $content)"
REM MinimumCompatibleClientVersion
powershell -Command "$content = Get-Content -Encoding ASCII '"%FILENAME%.hex"' -Raw; $index = $content.IndexOf('4D-69-6E-69-6D-75-6D-43-6F-6D-70-61-74-69-62-6C-65-43-6C-69-65-6E-74-56-65-72-73-69-6F-6E-'); $content = $content.Remove($index+90, 84);$content = $content.Insert($index+90, '03-05-00-00-00-01-00-00-00-12-00-00-00-1E-00-00-00-00-00-00-00-00-00-00-00-01-0F-00'); [System.IO.File]::WriteAllText('"%FILENAME%.hex"', $content)"

REM .hex ファイルをバイナリに変換して元のファイルに戻す
certutil -f -decodehex "%FILENAME%.hex" "%FILENAME%.new" > NUL

REM 編集後のファイルで元のファイルを上書き
copy /y "%FILENAME%.new" "%FILENAME%" > NUL

REM 一時ファイルの削除
del "%FILENAME%.hex" "%FILENAME%.new"

endlocal
exit /b 0
</pre>
</body>
</html>



